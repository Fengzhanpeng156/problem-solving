# 微控制器介绍

微控制器（microcontroller，uC或MCU）是一个小计算机，典型地包含CPU、RAM、存储固件代码的Flash，以及一些引脚。其中一些引脚为MCU供电，通常被标记为VCC和GND。其他引脚通过高低电压来与MCU通信，最简单的通信方法之一就是把一个LED接在引脚上：LED一端接地，另一端串接一个限流电阻，然后接到MCU信号引脚。在固件代码中设置引脚电压的高低就可以使LED闪烁

# 存储和寄存器

MCU的32位地址空间按区分割。例如，一些存储区被映射到特定的地址，这里是MCU的片内flash，固件代码指令在这些存储区读和执行。另一些区是RAM，也被映射到特定的地址，我们可以读或写任意值到RAM区。

从STM32F429数据手册的2.3.1节，我们可以了解到RAM区从地址0x20000000开始，共有192KB。从2.4节我们可以了解到flash被映射到0x08000000，共2MB，所以flash和RAM的位置像这样：

# 1 minimal

```c
/*main.c*/
int main(void) {
    return 0; // Do nothing so far
  }
  
  // Startup code
  __attribute__((naked, noreturn)) void _reset(void) {
    // memset .bss to zero, and copy .data section to RAM region
    extern long _sbss, _ebss, _sdata, _edata, _sidata;
    for (long *dst = &_sbss; dst < &_ebss; dst++) *dst = 0;
    for (long *dst = &_sdata, *src = &_sidata; dst < &_edata;) *dst++ = *src++;
  
    main();             // Call main()
    for (;;) (void) 0;  // Infinite loop in the case if main() returns
  }
  
  extern void _estack(void);  // Defined in link.ld
  
  // 16 standard and 91 STM32-specific handlers
  __attribute__((section(".vectors"))) void (*tab[16 + 91])(void) = {
    _estack, _reset
  };
```

| 符号      | 代表什么                                                |
| :-------- | :------------------------------------------------------ |
| `_sbss`   | `.bss段`（未初始化的全局/静态变量）的起始地址           |
| `_ebss`   | `.bss段`的结束地址                                      |
| `_sdata`  | `.data段`（有初始值的全局/静态变量）的在RAM中的起始地址 |
| `_edata`  | `.data段`在RAM中的结束地址                              |
| `_sidata` | `.data段`的初始值在ROM（Flash）中的起始地址             |

这些地址是编译时通过链接器 (`ld`) 脚本固定好的！

```c
/*link.ld*/
ENTRY(_reset);
MEMORY {
  flash(rx)  : ORIGIN = 0x08000000, LENGTH = 2048k
  sram(rwx) : ORIGIN = 0x20000000, LENGTH = 192k  /* remaining 64k in a separate address space */
}
_estack     = ORIGIN(sram) + LENGTH(sram);    /* stack points to end of SRAM */

SECTIONS {
  .vectors  : { KEEP(*(.vectors)) }   > flash
  .text     : { *(.text*) }           > flash
  .rodata   : { *(.rodata*) }         > flash

  .data : {
    _sdata = .;   /* .data section start */
    *(.first_data)
    *(.data SORT(.data.*))
    _edata = .;  /* .data section end */
  } > sram AT > flash
  _sidata = LOADADDR(.data);

  .bss : {
    _sbss = .;              /* .bss section start */
    *(.bss SORT(.bss.*) COMMON)
    _ebss = .;              /* .bss section end */
  } > sram

  . = ALIGN(8);
  _end = .;     /* for cmsis_gcc.h  */
}
```

`firmware.bin` 文件由3部分组成：`.vectors`(中断向量表)、`.text`(代码)、`.data`(数据)。这些部分根据链接脚本被分配到不同的存储空间：`.vectors` 在flash的最前面，`.text` 紧随其后，`.data` 则在那之后很远的地方。`.text` 中的地址在flash区，`.data` 在RAM区。例如，一个函数的地址是 `0x8000100`，则它位于flash中。而如果代码要访问 `.data` 中的变量，比如位于 `0x20000200`，那里将什么也没有，因为在启动时 `firmware.bin` 中 `.data` 还在flash里！这就是为什么必须要在启动代码中将 `.data` 区段拷贝到RAM。

现在我们可以生成完整的 `firmware.elf` 固件了：

```bash
arm-none-eabi-gcc -T link.ld -nostdlib main.o -o firmware.elf
```

| 部分                | 解释                                                         |
| :------------------ | :----------------------------------------------------------- |
| `arm-none-eabi-gcc` | 调用交叉编译器，编译 ARM Cortex-M 代码（比如 STM32）。       |
| `-T link.ld`        | 告诉编译器：**用 `link.ld` 这个链接脚本**。链接脚本决定内存布局。 |
| `-nostdlib`         | **不要自动链接标准库**（比如 libc、libgcc），因为我们是**裸机**（bare-metal）开发，不需要操作系统支持。 |
| `main.o`            | 你编译出来的目标文件（object file）。                        |
| `-o firmware.elf`   | 指定输出的文件名叫 `firmware.elf`。                          |

先把 `firmware.elf` 中各个区段抽取到一个连续二进制文件中：

```
arm-none-eabi-objcopy -O binary firmware.elf firmware.bin
```

然后使用 `st-link` 工具将 `firmware.bin` 烧入板子，连接好板子，然后执行：

```
st-flash --reset write firmware.bin 0x8000000
```